#!/usr/bin/env perl
#
# If an off-road flytipping report within Buckinghamshire has not been closed
# after three weeks, close it with a message. If it's older than six weeks,
# use a different message and suppress any alerts.

use v5.14;
use warnings;

BEGIN {
    use File::Basename qw(dirname);
    use File::Spec;
    my $d = dirname(File::Spec->rel2abs($0));
    require "$d/../../setenv.pl";
}

use constant BUCKS_NAME => 'Buckinghamshire Council';
use constant TIME_OPEN => '3 weeks';
use constant TIME_OPEN_ALERT => '6 weeks';

use FixMyStreet::DB;
use FixMyStreet::Script::ArchiveOldEnquiries;
use Getopt::Long::Descriptive;

my ($opts, $usage) = describe_options(
    '%c %o',
    ['commit|c', "actually close reports and send emails. Omitting this flag will do a dry-run"],
    ['help|h', "print usage message and exit" ],
);
print($usage->text), exit if $opts->help;

my $body = FixMyStreet::DB->resultset("Body")->search({ name => BUCKS_NAME })->first;
die "Could not find Bucks body" unless $body;

find_problems(TIME_OPEN_ALERT, TIME_OPEN, 'Auto-closure', 1);
find_problems(undef, TIME_OPEN_ALERT, 'Auto-closure (old)', 0);

sub find_problems {
    my ($from, $to, $title, $retain_alerts) = @_;

    my $template = FixMyStreet::DB->resultset("ResponseTemplate")->search({
        body_id => $body->id, title => $title,
    })->first;
    die "Could not find Bucks Flytipping template" unless $template;

    $to = "current_timestamp - '$to'::interval";
    my $time_param;
    if ($from) {
        $from = "current_timestamp - '$from'::interval";
        $time_param = [ -and => { '>=', \$from }, { '<', \$to } ],
    } else {
        $time_param = { '<', \$to };
    }

    # Fetch all Flytipping problems made off-road (i.e. those that previously
    # would been sent only to districts), between $from and $to
    my $q = FixMyStreet::DB->resultset("Problem")->search({
        bodies_str => $body->id,
        category => 'Flytipping (off-road)',
        state => [ FixMyStreet::DB::Result::Problem->open_states() ],
        confirmed => $time_param,
    });

    # Provide some variables to the archiving script
    FixMyStreet::Script::ArchiveOldEnquiries::update_options({
        user => $body->comment_user->id,
        closure_text => $template->text,
        retain_alerts => $retain_alerts,
        commit => $opts->commit,
    });

    # Close the reports
    FixMyStreet::Script::ArchiveOldEnquiries::close_problems($q);
}
